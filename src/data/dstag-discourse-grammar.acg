(* Requires the dstag-clausal-grammar.acg compilation first: *)
(* command: acgc.opt dstag-clausal-grammar.acg dstag-discourse-grammar.acg *)

signature discourse_grammar =
    DU,DUa,DU_v,T:type;
    NP,NP_cl,N,Adj,Det,S:type;
    NPa,Na,Va,Adja,Sa:type;
    
    DC = DUa-> DUa-> DUa-> DU-> DUa : type;

    DC_v = DUa-> DUa-> DUa-> DU_v-> DUa : type;


    In:Na;
    Iv:Va;
    Iadj:Adja;
    Is : Sa;
    
    d_fred,d_ceci,d_il : NP;
    d_l : NP_cl;
    d_etre: Sa -> Va -> Va -> NP -> Adj -> S;
    d_mauvaise_humeur,d_vide: Adja -> Adj;
    d_absente: Adja -> NP -> Adj;
    
    
    d_les,d_des,d_le,d_sa,d_une : N -> NP;
    d_clefs,d_cauchemards,d_permis,
	d_supermarche,d_frigo,d_cinema,d_femme,d_semaine : Na -> N;
    
    d_perdre,d_faire,d_rater,d_aimer: Sa -> Va -> Va -> NP -> NP -> S;
    d_aimer_cl : Sa -> Va -> Va -> NP -> NP_cl -> S;
    d_aller_a : Sa -> Va -> Va -> NP -> NP -> S;

    d_prouver: Sa -> Va -> Va -> NP -> Sa;
    
    d_avoir,d_etre_aux:Va -> Va;
    d_mal,d_beaucoup:Va -> Va;
    d_dormir: Sa -> Va -> Va -> NP -> S;

    d_parce_que:DC;
    d_de_plus: DC ;
    d_ensuite_s:DC;
    d_ensuite_v:DC_v;

    d_empty:DC;
    d_empty_comment:DC;


    d_anchor_s:S -> DUa -> DU;
    d_anchor_v:S -> DUa -> DU_v; 

    I_DU : DUa;

    CLOSE_DISCOURS : DU -> T;


    C1=d_etre Is Iv Iv d_fred (d_mauvaise_humeur Iadj) : S;
    C2'=d_perdre Is (d_beaucoup Iv) (d_avoir Iv) d_fred (d_les (d_clefs In)) :S;
    C2''=d_perdre Is Iv (d_avoir Iv) d_fred (d_une (d_clefs In)) :S;
    C2=d_perdre Is Iv (d_avoir Iv) d_fred (d_les (d_clefs In)) :S;
    C3=d_rater Is (d_beaucoup Iv) (d_avoir Iv) d_fred (d_le (d_permis In)) :S;
    C4=d_dormir Is (d_beaucoup Iv) (d_avoir (d_mal Iv)) d_fred :S;
    C4'=d_dormir Is Iv Iv d_fred :S;
    C5= d_faire Is (d_beaucoup Iv) (d_avoir Iv) d_fred (d_des (d_cauchemards In)):S;
    C6= d_aller_a Is (d_beaucoup Iv) (d_etre_aux Iv) d_fred (d_le (d_supermarche In)) :S;
    C7= d_etre Is Iv (d_beaucoup Iv) (d_le (d_frigo In)) (d_vide Iadj):S;
    C8= d_aller_a Is (d_beaucoup Iv) (d_etre_aux Iv) d_fred (d_le (d_cinema In)) :S;
    C9= d_etre Is Iv (d_beaucoup Iv) (d_sa (d_femme In)) (d_absente Iadj (d_le (d_semaine In))):S;
    C10= d_aimer_cl (d_prouver Is Iv Iv d_ceci) (d_beaucoup Iv) Iv d_il d_l :S;
	
    
end

nl_lexicon discourse_semantics(discourse_grammar):logic =
    NP,NP_cl := qnp;
    N := e=>t;
    Adj := e=>t;
    S := (t=>t) => t;
    Det := (e=>t) => qnp;

    NPa:= qnp => qnp;
    Na := (e=>t) => (e=>t);
    Va := t => t;
    Adja:= (e=>t) => (e=>t);
    Sa:= t => t;


    Iv,Iadj,Is,In:=Lambda x.x;

    (* To be changed for pronouns *)
    d_fred,d_il := Lambda P.P fred;
    d_l := Lambda P.ExUni x. (wife x fred) & (P x);
    d_ceci:= Lambda P.P  ceci;
    
    d_etre := Lambda S A1 A2 s adj mod. (S(s (Lambda x.A2 (mod (A1 (adj x))))));
    d_mauvaise_humeur := Lambda m.m bad_mood;
    d_absente := Lambda m P z .P(Lambda x. m (Lambda y. away y x) z)  ;
    d_vide := Lambda m.m empty;

    d_les,d_le,d_sa := Lambda P Q.ExUni x.(P x) & (Q x);
    d_des,d_une := Lambda P Q.Ex x.(P x) & (Q x);

    d_clefs:= noun key;
    d_cauchemards := noun nightmare;
    d_permis := noun license;
    d_supermarche := noun supermarket;
    d_frigo := noun fridge;
    d_cinema := noun movies ;
    d_femme := noun (Lambda x. wife x fred);
    d_semaine := noun week;

    d_perdre := transitive_verb lose;
    d_faire := transitive_verb have;
    d_rater := transitive_verb miss;
    d_aimer := transitive_verb love;
    d_aimer_cl := transitive_verb love;
    d_aller_a := transitive_verb go_to;

    d_prouver := Lambda S A1 A2 s c.S(s(Lambda x.A2 (A1 (prove x c))));

    d_dormir := intransitive_verb sleep;

    d_beaucoup := Lambda m P. m (a_lot P);
    d_mal := Lambda m P. m (badly P);

    d_etre_aux,d_avoir := Lambda m P.m (PAST P);

    DU := ttt;
    DU_v := (t=>t) => ttt;
    DUa := ttt => ttt;
    T := t;
    I_DU := Lambda x.x;    
    CLOSE_DISCOURS := Lambda D.D(Lambda x.x);
    
    d_parce_que := A' Explication;
    d_de_plus := A' Continuation;
    d_ensuite_s := B Narration;
    d_ensuite_v := 
	Lambda d4 d3 d2.
            Lambda d_subst.
		Lambda d_foot.
		    d4 (
			(my_phi' Narration)
			    (d3 d_foot)
				(d2 (d_subst (Lambda x.x)))
				    ) ;

    DU_v := (t=>t) => ttt;

(*    d_anchor_v := Lambda s mod adv.Lambda P.P (mod (Lambda Q.Q(s adv)) (Lambda y.y))  ;*)
    d_anchor_v := Lambda s mod adv.Lambda P.mod (Lambda Q.Q(s adv)) P  ;
    (*    d_anchor_s := Lambda s mod.Lambda P. P (mod (Lambda Q.Q (s (Lambda x.x))) (Lambda y.y)); *)
    d_anchor_s := Lambda s mod.Lambda P. mod (Lambda Q.Q (s (Lambda x.x))) P;
    d_empty_comment := A' Comment;
    d_empty := B Explication;

end

lexicon disc_to_sent(discourse_grammar):derivation_trees =


    DU:= S;
    DUa:= Sa;
    DU_v:= (Va -> Va)-> S;
    T := S;

    NP:=NP;
    NP_cl:=NP_cl;
    N:=N;
    Adj:=Adj;
    Det:=Det;
    NPa:=NPa;
    Na:=Na;
    Va:=Va -> Va;
    Adja:=Adja;
    Sa:=Sa;
    S:=(Va -> Va) -> S;
    
(*    Inp:=NPa; *)
    In:=In;
    Iv:=lambda P.P;
    Iadj:=Iadj;
    Is := Is;
    
    d_fred := c_fred;
    d_ceci := c_ceci;
    d_il :=c_il;
    d_l  := c_l ;
    d_etre := lambda sa va1 va2 np adj.lambda mod. c_etre sa (va2 (mod (va1 Iv))) np adj ;
    d_mauvaise_humeur:=c_mauvaise_humeur;
    d_vide:=c_vide;
    d_absente := c_absente;
    
    
    d_les:=c_les;
    d_des := c_des;
    d_le := c_le;
    d_sa := c_sa;
    d_une:=c_une;
    d_clefs:=c_clefs;
    d_cauchemards := c_cauchemards;
    d_permis := c_permis;
    d_supermarche := c_supermarche;
    d_frigo := c_frigo;
    d_cinema := c_cinema;
    d_femme := c_femme;
    d_semaine:=c_semaine;
    
    d_perdre:=transitive_verb c_perdre;
    d_faire := transitive_verb c_faire;
    d_rater := transitive_verb c_rater;
    d_aimer:=transitive_verb c_aimer;
    d_aimer_cl  := cl_transitive_verb c_aimer_cl ;
    d_aller_a  := transitive_verb c_aller_a ;
    
    d_prouver := pont c_prouver;
    
    d_avoir:=aux c_avoir;
    d_etre_aux:=aux c_etre_aux;
    d_mal:=v_adv c_mal;
    d_beaucoup:=v_adv  c_beaucoup;
    d_dormir := intransitive_verb c_dormir;
    
   
    CLOSE_DISCOURS := lambda x.x;
    I_DU := Is;
    d_ensuite_v :=  lambda dua1 dua2 dua3 s.
	c_discours_ensuite_v dua1 dua2 dua3 (s c_ensuite_v);
    d_ensuite_s := c_discours_ensuite_s ;
    d_de_plus :=  c_discours_de_plus;
    d_parce_que := c_discours_parce_que ;


    d_empty := c_discours_empty ;
    d_empty_comment := c_discours_empty_comment ;

    d_anchor_s := lambda s mod. c_anchor_discours mod (s(lambda x.x));
    d_anchor_v := lambda s mod adv. c_anchor_discours mod (s adv);

    
    



end

lexicon tag_discourse = surface << disc_to_sent
lexicon tag_discourse_derivation = syntax << disc_to_sent

lexicon direct_interpretation(discourse_grammar) : strings =
    T := 	string;
    Adj := 	string;
    Adja := 	string -> string;
    DU := 	string;
    DU_v := 	((string -> string) -> string -> string) -> string;
    DUa := 	string -> string;
    Det := 	string;
    N := 	string;
    NP := 	string;
    NP_cl := 	string;
    NPa := 	string -> string;
    Na := 	string -> string;
    S := 	((string -> string) -> string -> string) -> string;
    Sa := 	string -> string;
    T := 	string;
    Va := 	(string -> string) -> string -> string;
    I_DU := 	lambda x. x ;
    Iadj := 	lambda x. x ;
    In := 	lambda x. x ;
    Is := 	lambda x. x ;
    Iv := 	lambda P f z. P (lambda y.f y) z;
    CLOSE_DISCOURS := 	lambda x. x ;
    d_absente := 	lambda m n. m (lambda z. absente (n z)) ;
    d_aimer := 	lambda sa va1 va2 subj obj mod. sa (lambda z. subj (va2 (mod (va1 (lambda x. x))) aime (obj z))) ;
    d_aimer_cl := 	lambda sa va1 va2 subj obj mod. sa (lambda z. subj (obj (va2 (mod (va1 (lambda x. x))) aime z))) ;
    d_aller_a := 	lambda sa va1 va2 subj obj mod. sa (lambda z. subj (va2 (mod (va1 (lambda x. x))) alle (a (obj z)))) ;
    d_anchor_s := 	lambda s mod. mod (s (lambda x. x)) ;
    d_anchor_v := 	lambda s mod adv. mod (s adv) ;
    d_avoir := 	lambda va v x z. va v a (x z) ;
    d_beaucoup := 	lambda va v x. va v (lambda z. x (beaucoup z)) ;
    d_cauchemards := 	lambda a. a cauchemards ;
    d_ceci := 	ceci ;
    d_cinema := 	lambda a. a cinema ;
    d_clefs := 	lambda a. a clefs ;
    d_de_plus := 	lambda s1 s2 s3 s x. s1 (s2 (lambda z. x (de (plus (s3 s z))))) ;
    d_des := 	lambda x z. des (x z) ;
    d_dormir := 	lambda sa va1 va2 subj mod. sa (lambda z. subj (va2 (mod (va1 (lambda x. x))) dormi z)) ;
    d_empty := 	lambda d4 d3 d2 d_subst d. d4 (d3 (lambda z. d (DOT (d2 d_subst z)))) ;
    d_empty_comment := 	lambda d4 d3 d2 d_subst d. d4 (d3 (lambda z. d (d2 d_subst z))) ;
    d_ensuite_s := 	lambda s1 s2 s3 s x. s1 (s2 (lambda z. x (ensuite (s3 s z)))) ;
    d_ensuite_v := 	lambda dua1 dua2 dua3 s x. dua1 (dua2 (lambda z. x (dua3 (s (lambda A x'. A (lambda z'. x' (ensuite z')))) z))) ;
    d_etre := 	lambda sa va1 va2 np adj mod. sa (lambda z. np (va2 (mod (va1 (lambda x. x))) est (adj z))) ;
    d_etre_aux := 	lambda va v x z. va v est (x z) ;
    d_faire := 	lambda sa va1 va2 subj obj mod. sa (lambda z. subj (va2 (mod (va1 (lambda x. x))) fait (obj z))) ;
    d_femme := 	lambda a. a femme ;
    d_fred := 	Fred ;
    d_frigo := 	lambda a. a frigo ;
    d_il := 	il ;
    d_l := 	l ;
    d_le := 	lambda x z. le (x z) ;
    d_les := 	lambda x z. les (x z) ;
    d_mal := 	lambda va v x. va v (lambda z. x (mal z)) ;
    d_mauvaise_humeur := 	lambda m z. de (m (lambda z'. mauvaise (humeur z')) z) ;
    d_parce_que := 	lambda s1 s2 s3 s x. s1 (s2 (lambda z. x (parce (que (s3 s z))))) ;
    d_perdre := 	lambda sa va1 va2 subj obj mod. sa (lambda z. subj (va2 (mod (va1 (lambda x. x))) perdu (obj z))) ;
    d_permis := 	lambda a. a permis ;
    d_prouver := 	lambda sa va1 va2 subj x. sa (lambda z. subj (va2 (va1 (lambda x'. x')) prouve (que (x z)))) ;
    d_rater := 	lambda sa va1 va2 subj obj mod. sa (lambda z. subj (va2 (mod (va1 (lambda x. x))) rate (obj z))) ;
    d_sa := 	lambda x z. sa (x z) ;
    d_semaine := 	lambda a. a semaine ;
    d_supermarche := 	lambda a. a supermarche ;
    d_une := 	lambda x z. une (x z) ;
    d_vide := 	lambda m. m vide ;
end