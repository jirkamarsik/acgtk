signature derivations =
    S, DP, VP, PP, P , N :type ;

    c_john: DP;
    c_left: (VP -> VP) -> DP -> S;
    c_saw: DP -> DP -> S;
    
    c_city,c_person: (N -> N) -> N;

    c_everyone,c_someone,c_no_one: (DP -> S) -> S;

    c_some,c_every: N -> (DP -> S) -> S;

    (* Auxiliary trees *)

    c_with: DP -> VP  -> VP ;
    c_from : DP -> N -> N;
    c_from_2 : ((DP ->S) -> S) -> N -> N;
    id_vp : VP -> VP ;
    id_n : N -> N;
end



signature derived_trees =
    tree : type ;

    left,everyone,someone,no_one,John,with,from,every,some,city,person,saw:tree;

    DP_1, VP_1, PP_1, P_1, N_1:tree -> tree;
    S_2, DP_2, VP_2, PP_2, P_2, N_2:tree -> tree -> tree;

end


signature logic =
    e,t:type;

    j :e;
    left:e->t;
    city,person : e => t;
    saw,from:e->e->t;

    with : e -> (e->t)->(e ->t);

    binder ALL : (e=>t) -> t;
    binder Ex : (e=>t) -> t;
    
    infix > : t -> t -> t;
    infix & : t -> t -> t;



end


lexicon derivation_to_derived (derivations) : derived_trees =

    S, DP, VP, PP, P , N :=tree ;

    c_john:= DP_1 John;
    c_left:= lambda a s.S_2 s (a (VP_1 left));
    c_saw := lambda s o . S_2 (DP_1 s) (VP_2 saw (DP_1 o));
    
    c_city:= lambda a. a (N_1 city);
    c_person:= lambda a. a (N_1 person);

    c_everyone := lambda t. t(DP_1 everyone);
    c_someone := lambda t.t(DP_1 someone);
    c_no_one:= lambda t.t(DP_1 no_one);

    c_some := lambda n. lambda t.t(DP_2 some n);
    c_every:= lambda n. lambda t.t(DP_2 every n);

    (* Auxiliary trees *)

    c_with:= lambda dp vp.VP_2 vp (PP_2 (P_1 with) dp);
    c_from := lambda dp n.N_2 n (PP_2 (P_1 from) dp);
    c_from_2 := lambda dp n.dp (lambda y. N_2 n (PP_2 (P_1 from) y));
    id_vp := lambda x.x;
    id_n := lambda x.x;
end


lexicon sem (derivations) : logic =

    S :=t;
    DP := e;
    VP := e -> t;
    PP := e => t;
    P:= e => e => t;
    N :=e=>t;

    c_john:= j;
    c_left:= lambda a s. a left s;

    c_saw := lambda s o . saw s o ;
    
    c_city:= lambda a. a city;
    c_person:= lambda a. a person;

    c_everyone := lambda P. ALL x.P x;
    c_someone := lambda P. Ex x.P x;
    c_no_one:= lambda P. Ex x.P x;

    c_some := lambda n. lambda P. Ex x. (n x) & (P x);
    c_every:= lambda n. lambda P. ALL x. (n x) > (P x);

    (* Auxiliary trees *)

    c_with:= lambda dp vp . (with dp) vp;
    c_from := lambda dp n.Lambda x. (n x) & (from dp x);
    (*    c_from_2 := lambda dp n.Lambda x.dp (lambda y. (n x) & (from y x));*)
    c_from_2 := lambda dp n.Lambda x.(n x) & dp (lambda y. (from y x)); 
    id_vp := lambda x.x;
    id_n := lambda x.x;
end

    
    