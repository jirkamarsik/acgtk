signature derivation_trees =
    NP,NP_cl,N,Adj,S,Det:type;
    NPa,Na,Va,Adja,Sa:type;
    Inp:NPa;
    In:Na;
    Iv:Va;
    Iadj:Adja;
    Is : Sa;
    
    c_fred,c_ceci,c_il : NP;
    c_l : NP_cl;
    c_etre: Sa -> Va -> NP -> Adj -> S;
    c_mauvaise_humeur,c_vide: Adja -> Adj;
    c_absente: Adja -> NP -> Adj;
    
    
    c_les,c_des,c_le,c_sa,c_une : N -> NP;
    c_clefs,c_cauchemards,c_permis,
	c_supermarche,c_frigo,c_cinema,c_femme,c_semaine : Na -> N;
    
    c_perdre,c_faire,c_rater,c_aimer: Sa -> Va -> NP -> NP -> S;
    c_aimer_cl : Sa -> Va -> NP -> NP_cl -> S;
    c_aller_a : Sa -> Va -> NP -> NP -> S;

    c_prouver: Sa -> Va -> NP -> Sa;
    
    c_avoir,c_etre_aux:Va -> Va;
    c_mal,c_beaucoup:Va -> Va;
    c_dormir: Sa -> Va -> NP -> S;

    c_parce_que:Sa;
    c_de_plus: Sa ;
    c_ensuite_s:Sa;
    c_ensuite_v:Va -> Va;

    c_discours_ensuite_s: Sa -> Sa -> Sa -> S -> Sa;
    c_discours_ensuite_v: Sa -> Sa -> Sa -> S -> Sa;
    c_discours_parce_que: Sa -> Sa -> Sa -> S -> Sa;
    c_discours_de_plus: Sa -> Sa -> Sa -> S -> Sa;
    c_discours_empty: Sa -> Sa -> Sa -> S -> Sa;
    c_discours_empty_comment: Sa -> Sa -> Sa -> S -> Sa;

    c_anchor_discours: Sa -> S -> S;

    transitive_verb=
	lambda v.
	    lambda sa va1 va2 subj obj.
		lambda mod.
		    v sa (va2 (mod (va1 Iv))) subj obj:
			(Sa -> Va -> NP -> NP -> S) ->
			    Sa -> (Va -> Va) -> (Va -> Va) -> NP -> NP ->
				(Va -> Va) -> S;

    cl_transitive_verb=
	lambda v.
	    lambda sa va1 va2 subj obj.
		lambda mod.
		    v sa (va2 (mod (va1 Iv))) subj obj:
			(Sa -> Va -> NP -> NP_cl -> S) ->
			    Sa -> (Va -> Va) -> (Va -> Va) -> NP -> NP_cl ->
				(Va -> Va) -> S;


    intransitive_verb=
	lambda v.
	    lambda sa va1 va2 subj.
		lambda mod.
		    v sa (va2 (mod (va1 Iv))) subj:
			(Sa -> Va -> NP -> S) ->
			    Sa -> (Va -> Va) -> (Va -> Va) -> NP ->
				(Va -> Va) -> S;

    pont =
	lambda v.
	    lambda sa va1 va2 subj.v sa (va2 (va1 Iv)) subj :
		(Sa -> Va -> NP -> Sa) ->
		    Sa -> (Va -> Va) -> (Va -> Va) -> NP -> Sa;

    aux = lambda aux.
	lambda va v .aux (va v) :
	    (Va -> Va) -> 
		(Va -> Va) -> Va -> Va;

    v_adv = lambda adv.
	lambda va v. adv (va v) :
	    (Va -> Va) -> 
		(Va -> Va) -> Va -> Va;

    I_va = lambda x.x : Va -> Va;
    I_va_va = lambda x.x : (Va -> Va) -> Va -> Va;
end

signature elementary_trees =
    tree:type;
    N1,NP1,V1,Adv1,Aux1,Adj1,S1,Det1,Prep1: tree -> tree;
    N2,NP2,PP2,V2,Adj2,S2:tree -> tree -> tree;
    V3,S3: tree -> tree -> tree -> tree;
    S4: tree -> tree -> tree -> tree -> tree;

    Id=lambda x.x:tree -> tree;

    dot,comma:tree;

    transitive_verb=
	lambda v. lambda S A s o.S(S3 s (A (V1 v)) o):
	tree ->
	(tree -> tree) ->
	(tree -> tree) ->
	tree ->
	tree ->
	tree ;


    cl_transitive_verb=
	lambda v. lambda S A s o.S(S3 s o (A (V1 v)) ):
	tree ->
	(tree -> tree) ->
	(tree -> tree) ->
	tree ->
	tree ->
	tree ;
    
    intransitive_verb=
	lambda v. lambda S A s.S(S2 s (A (V1 v))):
	tree ->
	(tree -> tree) ->
	(tree -> tree) ->
	tree ->
	tree ;
    
    aux =
	lambda a. lambda A x. V2 (A(Aux1 a)) x :
	tree ->
	(tree -> tree) ->
	tree ->
	tree ;
    
    v_adv = lambda a. lambda A x. A(V2 x (Adv1 a)) :
	tree ->
	(tree -> tree) ->
	tree ->
	tree ;
    
    s_adv = lambda a. lambda S x. S(S2 (Adv1 a) x) :
	tree ->
	(tree -> tree) ->
	tree ->
	tree ;

    que:tree;

    pont=
	lambda v. lambda S A s x.S(S3 s (A (V1 v)) (S2 que x)):
	tree ->
	(tree -> tree) ->
	(tree -> tree) ->
	tree ->
	tree ->
	tree ;

    c_discours = lambda c. lambda s1 s2 s3 s x.
	s1 (s2 (S3 x c (s3 s))) :
	    tree ->
		(tree -> tree) ->
		    (tree -> tree) ->
			(tree -> tree) ->
			    tree ->
				tree -> tree;

    c_discours_v = lambda s1 s2 s3 s x.
	s1 (s2 (S2 x (s3 s))) :
	    (tree -> tree) ->
		(tree -> tree) ->
		    (tree -> tree) ->
			tree ->
			    tree -> tree;
    
    
    adverbiaux_s = lambda a.
	lambda d4 d3 d2 d_subst d.
	    d4 (S1 (d3 (S4 d dot a (d2 d_subst)))) :
		tree->
		    (tree-> tree)->
			(tree-> tree)->
			    (tree-> tree)->
				tree->
				    tree->
					tree ;
    
    conjonction_postposees_ponct = lambda c.
	lambda d4 d3 d2 d_subst d.
	    d4 (S1 (d3 (S4 d comma c (d2 d_subst)))) :
		tree->
		    (tree-> tree)->
			(tree-> tree)->
			    (tree-> tree)->
				tree->
				    tree->
					tree ;
    
    conjonction_postposees_s = lambda c.
	lambda d4 d3 d2 d_subst d.
	    d4 (S1 (d3 (S3 d c (d2 d_subst)))) :
		tree->
		    (tree-> tree)->
			(tree-> tree)->
			    (tree-> tree)->
				tree->
				    tree->
					tree ;
    


    
    noun = lambda n. lambda a. a (N1 n) : tree -> (tree -> tree) -> tree;
    det = lambda det. lambda x. NP2 (Det1 det) x : tree -> tree -> tree ;
    l_adj = lambda a. lambda A x. A (N2 a x) : tree -> (tree -> tree) -> tree -> tree;
    
    Fred,a,est,mauvaise,humeur,perdu,les,clefs,rate,le,permis,de,conduire,
	mal,dormi,fait,des,cauchemards,alle,au,supermarche,frigo,vide,cinema,
	sa,femme,absente,une,semaine,ceci,prouve,il,l,aime,beaucoup,
	de_plus,ensuite,parce_que:tree;

    epsilon:tree;

    
    
end

lexicon syntax(derivation_trees):elementary_trees =
    NP,NP_cl,N,Adj,S,Det:=tree;

    NPa,Na,Va,Adja,Sa:=tree -> tree;

    Iadj,Iv,In,Inp,Is := lambda x.x;

    c_fred:= NP1 Fred;
    c_ceci := NP1 ceci;
    c_il := NP1 il;
    c_l := NP1 l;
    c_etre:= lambda S A s adj. S(S3 s (A (V1 est)) adj);
    c_mauvaise_humeur:= lambda m. PP2 (Prep1 de) ((noun humeur) (l_adj mauvaise m));
    c_vide := lambda m.m(Adj1 vide);

    c_absente := lambda m n .m (Adj2 (Adj1 absente) n) ;

    c_les := det les;
    c_des := det des;
    c_le := det le;
    c_sa := det sa;
    c_une := det une;
    c_clefs:= noun clefs;
    c_frigo:= noun frigo;
    c_cinema:= noun cinema;
    c_permis:= noun permis;
    c_supermarche:= noun supermarche;
    c_cauchemards:= noun cauchemards;
    c_femme:= noun femme;
    c_semaine:= noun semaine;

    c_perdre:=transitive_verb perdu;
    c_faire:=transitive_verb fait;
    c_rater:=transitive_verb rate;
    c_aimer:=transitive_verb aime;
    c_aimer_cl:=cl_transitive_verb aime;

    c_aller_a:=  lambda S A s c.S(S3 s (A (V1 alle)) (PP2 (Prep1 a) c));

    c_avoir:= aux a;
    c_etre_aux:= aux est;
    c_mal:= v_adv mal;
    c_beaucoup:= v_adv beaucoup;
    c_dormir:= intransitive_verb dormi;

    c_prouver:= pont prouve;

    c_ensuite_s := s_adv ensuite Id;
    c_ensuite_v := v_adv ensuite;
    c_de_plus := s_adv de_plus Id;
    c_parce_que := s_adv parce_que Id;

    c_discours_ensuite_s := c_discours (Adv1 ensuite) ;
    c_discours_de_plus := c_discours (Adv1 de_plus) ;
    c_discours_parce_que := c_discours (Adv1 parce_que) ;
    c_discours_ensuite_v := c_discours_v;


    c_discours_empty_comment := conjonction_postposees_s epsilon;
    c_discours_empty := adverbiaux_s epsilon;

    c_anchor_discours := lambda m s.m s;


end


signature strings =
    o:type;
    string = o->o:type;
    infix + = lambda a b z.a (b z):string -> string -> string;
    binary = lambda x y. x + y: string -> string -> string ;
    ternary = lambda x y z. x + y + z:  string -> string -> string -> string;
    E=lambda x.x:string;
    
    Fred,a,est,mauvaise,humeur,perdu,les,clefs,rate,le,permis,de,conduire,
	mal,dormi,fait,des,cauchemards,alle,au,supermarche,frigo,vide,cinema,
	sa,femme,absente,une,semaine,ceci,prouve,que,il,l,aime,beaucoup,
	plus,ensuite,parce:string;

    DOT,COMMA:string;
end


lexicon yield(elementary_trees):strings =
    tree := string;
    S3,
	V3 := ternary;
    N2,NP2,PP2,V2,Adj2,S2 := binary;
    N1,NP1,V1,Adv1,Aux1,Adj1,S1,Det1,Prep1 := lambda x.x;
    S4 := lambda x y z t.x+y+z+t;

    epsilon:=lambda x.x;
    dot:=DOT;
    comma:=COMMA;
    Fred := Fred;
    a := a;
    est := est;
    mauvaise:=mauvaise;
    humeur := humeur;
    perdu := perdu;
    les := les;
    clefs := clefs;
    rate := rate;
    le := le;
    permis := permis;
    de := de;
    conduire := conduire;
    mal := mal;
    dormi := dormi;
    fait := fait;
    des := des;
    cauchemards := cauchemards;
    alle := alle;
    au := au;
    supermarche := supermarche;
    frigo := frigo;
    vide := vide;
    cinema := cinema;
    sa := sa;
    femme := femme;
    absente := absente;
    une := une;
    semaine := semaine;
    ceci := ceci;
    prouve := prouve;
    que := que;
    il := il;
    l := l;
    aime := aime;
    beaucoup:=beaucoup;

    parce_que := parce+que;
    ensuite:=ensuite;
    de_plus:=de+plus


end

lexicon surface = yield << syntax


signature logic =
    e,t:type;

    ttt=(t => t) -> t:type;

    qnp=(e=>t)->t:type;
    infix & : t -> t -> t;	
	
    (* Implications*)
    infix > : t -> t -> t;
    
    (* Quantifiers *)
    binder All : (e=>t) -> t;
    binder Ex : (e=>t) -> t;
    binder ExUni : (e=>t) -> t;

    fred,ceci,il,elle:e;
    bad_mood:e->t;
    away:e-> e -> t;
    empty:e->t;
    key,nightmare,license,supermarket,fridge,movies,week:e->t;
    wife:e->e->t;

    lose,have,miss,love,go_to:e->e->t;

    prove:e->t->t;

    PAST:t->t;
    
    sleep:e->t;
    badly,a_lot:t->t;

    Explication : t=> t=> t ;
    Continuation : t=> t=> t ;	
    Narration : t=> t=> t ;	
    Comment : t => t => t;


    missing_arg : (t=>t=>t) -> t->t;

    transitive_verb =
	lambda v. lambda S A s o.S(s(Lambda x.o(Lambda y.A(v x y)))):
	    (e->e->t) -> (t->t) -> (t->t) -> qnp -> qnp -> t;

    intransitive_verb =
	lambda v. lambda S A s.S(s(Lambda x.A(v x))):
	    (e->t) -> (t->t) -> (t->t) -> qnp -> t;

    noun = lambda n. lambda m. m (Lambda x.n x):
	(e->t) -> ((e=>t)->(e=>t)) -> (e=>t);


    phi'' = lambda R X Y P.X(Lambda x.Y(Lambda y.(P x) & (R x y))):
	(t => t=> t)-> ((t => t)-> t)-> ((t => t)-> t)-> (t=> t)-> t;
    
    my_phi' = Lambda R. lambda X Y P.X(Lambda x.Y(Lambda y. P (R x y))):
	(t => t=> t)=> ((t => t)-> t)-> ((t => t)-> t)-> (t=> t)-> t;
    
    B = lambda R.
	lambda d4 d3 d2.
            lambda d_subst.
		lambda d_foot.
		    d4 (
			(phi'' R)
			    (d3 d_foot)
				(d2 d_subst)
				    ) :
					(t => t => t) ->
					    (ttt -> ttt) ->
						(ttt -> ttt) -> 
						    (ttt -> ttt) ->
							ttt ->
							    (ttt -> ttt);
    
    A' = Lambda R.
	lambda d4 d3 d2.
            lambda d_subst.
		lambda d_foot.
		    d4 (
			(my_phi' R)
			    (d3 d_foot)
				(d2 d_subst)
				    ) :
					(t => t => t)  =>
					    (ttt -> ttt) ->
						(ttt -> ttt) ->
						    (ttt -> ttt) ->
							ttt ->
							    (ttt -> ttt);
    

    cont = Lambda t.lambda P.P t:t => (t=>t) -> t;


end

(* lexicon semantics(derivation_trees):logic =
    NP,NP_cl := qnp;
    N := e=>t;
    Adj := e->t;
    S := t;
    Det := (e->t) -> qnp;

    NPa:= qnp -> qnp;
    Na := (e=>t) -> (e=>t);
    Va := t -> t;
    Adja:= (e->t) -> (e->t);
    Sa:= t -> t;


    Inp,Iv,Iadj,Is,In:=lambda x.x;

    (* To be changed for pronouns *)
    c_fred,c_il := lambda P.P fred;
    c_l := lambda P.ExUni x. (wife x fred) & (P x);
    c_ceci:= lambda P.P  ceci;
    
    c_etre := lambda S A s adj. S(s (Lambda x.A(adj x)));
    c_mauvaise_humeur := lambda m.m bad_mood;
    c_absente := lambda m P z .P(Lambda x. m (lambda y. away y x) z)  ;
    c_vide := lambda m.m empty;

    c_les,c_le,c_sa := lambda P Q.ExUni x.(P x) & (Q x);
    c_des,c_une := lambda P Q.Ex x.(P x) & (Q x);

    c_clefs:= noun key;
    c_cauchemards := noun nightmare;
    c_permis := noun license;
    c_supermarche := noun supermarket;
    c_frigo := noun fridge;
    c_cinema := noun movies ;
    c_femme := noun (lambda x. wife x fred);
    c_semaine := noun week;

    c_perdre := transitive_verb lose;
    c_faire := transitive_verb have;
    c_rater := transitive_verb miss;
    c_aimer := transitive_verb love;
    c_aimer_cl := transitive_verb love;
    c_aller_a := transitive_verb go_to;

    c_prouver := lambda S A s c.S(s(Lambda x.A(prove x c)));

    c_dormir := intransitive_verb sleep;

    c_beaucoup := lambda m P. m (a_lot P);
    c_mal := lambda m P. m (badly P);

    c_etre_aux,c_avoir := lambda m P.m (PAST P);

    c_parce_que := lambda s. missing_arg Explication s;
    c_de_plus := missing_arg Continuation;
    c_ensuite_s := missing_arg Narration;
    c_ensuite_v := lambda P.missing_arg Narration P ;


    I_du := lambda f x. f (lambda y.y) x;



end
*)