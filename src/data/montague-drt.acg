(**************************************************************************)
(*                                                                        *)
(*                 ACG development toolkit                                *)
(*                                                                        *)
(*                  Copyright 2008 INRIA                                  *)
(*                                                                        *)
(*  More information on "http://acg.gforge.inria.fr/"                     *)
(*  License: CeCILL, see the LICENSE file or "http://www.cecill.info"     *)
(*  Authors: see the AUTHORS file                                         *)
(*                                                                        *)
(*                                                                        *)
(*                                                                        *)
(*                                                                        *)
(*  $Rev::                              $:  Revision of last commit       *)
(*  $Author::                           $:  Author of last commit         *)
(*  $Date::                             $:  Date of last commit           *)
(*                                                                        *)
(**************************************************************************)

signature abstract =
	np,s,n,s':type;	
	J,M,he,her,it:np;
	man,woman,farmer,donkey:n;
	loves,beats,owns : np => np => s;
	a,every,most:n=>np; 
	who: (np => s) => n => n;

	CLOSE : s => s';
	
end


signature object =
	g,e,t:type;
	o = g => (g => t) => t:type;
	sel : g => e;
	infix + : e => g => g;
	Top,Bot:t;
	john,mary : e;
	nil : g;
	man,woman,farmer,donkey:e=>t;
	infix & : t => t => t;
	love,beat,own : e => e => t;
	vt = Lambda v O S.S(Lambda x.O(Lambda y.Lambda e phi.(v x y) & (phi e))) : (e => e => t) => ((e =>o ) => o)  => ((e =>o ) => o) => o ;
	n = Lambda n .Lambda x.Lambda e phi.(n x) & (phi e) : (e => t) => e => o;
	binder Ex : (e => t) => t ;
	binder All : (e => t) => t ;
	binder Most : (e => o) => o ;
	prefix - : t => t ;
	binder Ex_dyn = Lambda P . Lambda e phi.Ex x. P x (x+e) phi : (e => o) => o ;
	prefix ~ = Lambda A . Lambda e phi. (-(A e (Lambda e.Top)))&(phi e) : o => o;
	infix @ = Lambda A B.Lambda e phi.A e (Lambda e.B e phi) : o => o => o;
	binder All_dyn = Lambda P.~(Ex_dyn x. ~(P x)) : (e =>o) => o; 
	infix > = Lambda A B . ~(A @ ~B) : o => o => o;
end


lexicon montague (abstract) : object =
	s := o;
	s' := t;
	np := (e => o) => o;
	n := e => o;
	J := Lambda P.P john;
	M := Lambda P.P mary;
	farmer := n farmer;
	woman := n woman ;
	man := n man ;
	donkey := n donkey ;
	loves := vt love ;
	beats := vt beat ;
	owns := vt own ;
	he,her,it := Lambda P.Lambda e phi. P (sel e) e phi;
	a := Lambda P Q . Ex_dyn x. (P x) @ (Q x);
	every := Lambda P Q . All_dyn x. (P x) > (Q x);
	most := Lambda P Q . Most x. (P x) > (Q x);
	who := Lambda R Q x. (Q x) @ (R (Lambda P.P x));

	CLOSE := Lambda S.S nil (Lambda e.Top);
end