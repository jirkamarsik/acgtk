(* requires a compilation command line including dstag-sandro.acg.
   For instance:
   acgc dstag-sandro.acgc dstag-sandro-discourse-derived-trees.acg *)


signature DERIVED_TREES =
    tree : type;
    
    DU_1,DC : tree-> tree;
    
    DU_3 : tree-> tree-> tree-> tree ;
    DU_4 : tree-> tree-> tree-> tree-> tree ;
    
    dot,comma:tree;
    
    parce_que:tree;
    
    de_plus : tree;
    
    epsilon : tree;
    
    ensuite : tree;
    
    c1,c2,c3,c4,c5,c6,c7,c8,c9,c10:tree;
    
    I = lambda x.x : tree -> tree;
    
    adverbiaux = lambda a.
	lambda d4 d3 d2 d_subst d.
	    d4 (DU_1 (d3 (DU_4 d dot a (d2 d_subst)))) :
		tree->
		    (tree-> tree)->
			(tree-> tree)->
			    (tree-> tree)->
				tree->
				    tree->
					tree ;
    
    conjonction_postposees_ponct = lambda c.
	lambda d4 d3 d2 d_subst d.
	    d4 (DU_1 (d3 (DU_4 d comma c (d2 d_subst)))) :
		tree->
		    (tree-> tree)->
			(tree-> tree)->
			    (tree-> tree)->
				tree->
				    tree->
					tree ;
    
    conjonction_postposees = lambda c.
	lambda d4 d3 d2 d_subst d.
	    d4 (DU_1 (d3 (DU_3 d c (d2 d_subst)))) :
		tree->
		    (tree-> tree)->
			(tree-> tree)->
			    (tree-> tree)->
				tree->
				    tree->
					tree ;
    
    
end

signature STRINGS =
    string : type;
    
    infix + : string-> string-> string;
    
    dot,comma : string;
    
    parce_que,de_plus,epsilon,ensuite : string;
    
    fred_est_de_mauvaise_humeur,
	il_a_perdu_ses_clefs,
	il_a_rate_son_permis,
	il_a_mal_dormi,
	il_a_fait_des_cauchemards,
	fred_est_alle_au_supermarche,
	son_frigo_etait_vide,
	il_est_alle_au_cinema,
	sa_femme_est_absente_une_semaine,
	ceci_prouve_qu_il_l_aime_beaucoup:string;
    
    fmh = fred_est_de_mauvaise_humeur:string ;
    psc = il_a_perdu_ses_clefs:string ;
    rsp = il_a_rate_son_permis:string ;
    md = il_a_mal_dormi:string;
    fc = il_a_fait_des_cauchemards:string;
    fasm = fred_est_alle_au_supermarche:string;
    fev = son_frigo_etait_vide:string;
    aac = il_est_alle_au_cinema:string;
    fas = sa_femme_est_absente_une_semaine:string;
    pab = ceci_prouve_qu_il_l_aime_beaucoup:string;
    
end

lexicon YIELD (DERIVED_TREES) : STRINGS =
    
    tree := string;
    
    DU_1,DC := lambda x.x;
    
    DU_3 := lambda x1 x2 x3. x1 + x2 + x3 ;
    DU_4 := lambda x1 x2 x3 x4. x1 + x2 + x3 + x4 ;
    
    dot := dot;
    comma := comma;
    parce_que := parce_que;
    de_plus := de_plus;
    epsilon := epsilon;
    ensuite := ensuite;
    
    c1 := fmh;
    c2 := psc;
    c3 := rsp;
    c4 := md;
    c5 := fc;
    c6 := fasm;
    c7 := fev;
    c8 := aac;
    c9 := fas;
    c10 := pab;
end




lexicon SYNTAX (DERIVATION_TREES): DERIVED_TREES =
    DU := tree;
    DU_a:= tree-> tree;
    T := tree;
    
    PARCE_QUE := conjonction_postposees parce_que;
    DE_PLUS := adverbiaux de_plus;
    
    EMPTY := adverbiaux epsilon;
    ENSUITE := conjonction_postposees_ponct ensuite;
    EMPTY_COMMENT := conjonction_postposees epsilon;
    
    I_DU := I;
    
    C1 := lambda x . x c1;
    C2 := lambda x . x c2;
    C3 := lambda x . x c3;
    C4 := lambda x . x c4;
    C5 := lambda x . x c5;
    C6 := lambda x . x c6;
    C7 := lambda x . x c7;
    C8 := lambda x . x c8;
    C9 := lambda x . x c9;
    C10 := lambda x . x c10;

    CLOSE_DISCOURS := I;
end

lexicon NORMALIZED_D = YIELD << SYNTAX 