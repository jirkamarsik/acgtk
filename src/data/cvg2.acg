(**************************************************************************)
(*                                                                        *)
(*                 ACG development toolkit                                *)
(*                                                                        *)
(*                  Copyright 2008 INRIA                                  *)
(*                                                                        *)
(*  More information on "http://acg.gforge.inria.fr/"                     *)
(*  License: CeCILL, see the LICENSE file or "http://www.cecill.info"     *)
(*  Authors: see the AUTHORS file                                         *)
(*                                                                        *)
(*                                                                        *)
(*                                                                        *)
(*                                                                        *)
(*  $Rev::                              $:  Revision of last commit       *)
(*  $Author::                           $:  Author of last commit         *)
(*  $Date::                             $:  Date of last commit           *)
(*                                                                        *)
(**************************************************************************)

signature syntax =

	NP,S,NP_S_Q,Q : type;

	G : NP_S_Q -> (NP -> S) -> Q;


	CHRIS,KIM,DANA,SANDY,EV,SO:NP;

	LIKE:NP -> NP -> S;
	BARKE:NP -> S;
	THOUGHT : S -> NP -> S;
	WONDERED : Q -> NP -> S;
	WHETHER : S -> S;

	WHO_IN_SITU : NP;
	WHAT_IN_SITU : NP;
	WHO_FILLER,WHAT_FILLER: NP_S_Q;


end

signature simple_syntax =
	NP,S,Q:type;
	everybody,somebody:NP;
	barked : NP -> S;
	liked:NP -> NP -> S;
	thought: S -> NP -> S;
	wondered : Q -> NP -> S;
	whether : S -> S;

	Chris,Kim,Dana,Sandy : NP;

	who_in_situ,what_in_situ:NP;
	who_filler,what_filler: (NP -> S) -> Q;

	
end

lexicon CVG_syntax (syntax) : simple_syntax =
	NP := NP;
	S := S;
	Q := Q;
	NP_S_Q := (NP ->S)->Q;

	G := lambda q r.q r;


	EV := everybody;
	SO := somebody;
	LIKE := liked;

	WHAT_IN_SITU := what_in_situ ;
	WHO_IN_SITU := who_in_situ ;
	WHETHER := lambda s . whether s;
	WONDERED := lambda q subj . wondered q subj ;
	THOUGHT := lambda s subj . thought s subj ;
	BARKE := barked ;

	SANDY := Sandy;
	DANA := Dana;
	KIM := Kim;
	CHRIS := Chris;
	
	WHAT_FILLER := lambda r. what_filler (lambda x .r x);
	WHO_FILLER := lambda r. who_filler (lambda x .r x);


end

lexicon CVG_strings (simple_syntax) : strings =
	NP,S,Q:= string;

	everybody := everyone;
	somebody := someone;
	barked := lambda x. x + barked;
	liked := lambda o s . s + liked + o ;
	thought := lambda s x. x + thought + s;
	wondered := lambda s x.x+wondered+s ;
	whether := lambda s.whether+s ;
	Chris := Chris ;
	Kim := Kim;
	Dana := Dana ;
	Sandy := Sandy ;

	who_in_situ := who;
	who_filler := lambda P . who + (P E);
	what_in_situ := what;
	what_filler := lambda P. what + (P E);
end


signature simple_semantics =
	e,t:type;

	everybody',somebody': e ;
	chris',kim',dana',sandy':e;

	like' : e -> e -> t;
	barke' : e -> t;
	think' : t -> e -> t;
	wonder' : t -> e -> t;


	whether' : t -> t;
	who_filler : (e -> t) -> t;
	who_in_situ : e ;
	what_filler : (e -> t) -> t;
	what_in_situ : e;
end

lexicon CVG_simple_semantics (syntax) : simple_semantics =
	NP := e;
	S := t;
	Q := t;

	NP_S_Q := (e -> t) -> t ;

	G := lambda q r.q r;


	EV := everybody';
	SO := somebody';

	CHRIS := chris';
	KIM := kim';
	DANA := dana';
	SANDY := sandy';

	LIKE := lambda o s .like' s o;
	BARKE := barke' ;
	THOUGHT := think' ;
	WONDERED := wonder' ;

	WHETHER := whether' ;

	WHO_IN_SITU := who_in_situ ;
	WHAT_IN_SITU := what_in_situ ;
	WHO_FILLER := who_filler ;
	WHAT_FILLER := what_filler ;
	
	

end
	

signature semantics =
	e,t,e_t_t:type;
	
	k,k1,k2,k3,e_t_k1,e_k1_k2,e_k2_k3:type;

	G_q : e_t_t -> (e ->t) -> t;
	G_w12 : e_k1_k2 -> (e -> k1) -> k2;
	G_w23 : e_k2_k3 -> (e -> k2) -> k3;
	

	everybody',somebody': e_t_t;
	chris',kim',dana',sandy':e;

	like' : e -> e -> t;
	barke' : e -> t;
	think' : t -> e -> t;

	wonder1' : k1 -> e -> t;
	wonder2' : k2 -> e -> t;


	whether' : t -> k;

	who_filler : (e -> t) -> k1;
	who_in_situ12 : e_k1_k2 ;
	who_in_situ23 : e_k2_k3 ;
	what_filler :  (e -> t) -> k1;
	what_in_situ12 : e_k1_k2 ;
	what_in_situ23 : e_k2_k3 ;
end


lexicon CVG_semantics (semantics):simple_semantics =
	e:=e;
	t:=t;
	e_t_t:=e;

	k,k1,k2,k3:=t;
	e_t_k1,e_k1_k2,e_k2_k3:=e;

	G_q := lambda Q r.r Q;
	G_w12 := lambda w r.r w;
	G_w23 := lambda w r.r w;

	everybody' := everybody';
	somebody' := somebody';
	chris' := chris';
	kim' := kim';
	dana' := dana';
	sandy' := sandy';

	like' := like';
	barke' := barke';
	think' := think';

	wonder1' := wonder';
	wonder2' := wonder';

	whether' := whether';

	who_filler := who_filler ;
	who_in_situ12 := who_in_situ ;
	who_in_situ23 := who_in_situ ;
	what_filler := what_filler ;
	what_in_situ12 := what_in_situ ;
	what_in_situ23 := what_in_situ ;
end

signature ty_n =
	e,t:type;

	everybody',somebody': (e -> t) -> t ;
	chris',kim',dana',sandy':e;
	
	person',thing' : e -> t;
	whether' : t -> t -> t;
	
	infix & : t -> t -> t;
	infix | : t -> t -> t; 

	which0 : (e ->t) -> (e -> t) -> e -> t -> t ;
	which1 : (e ->t) -> (e -> e -> t -> t) -> e -> e -> t -> t ;
	which2 : (e ->t) -> (e -> e -> e -> t -> t) -> e -> e -> e -> t -> t ;

	who = which0 person' : (e -> t) -> e -> t -> t ;
	who12 = which1 person' : (e -> e -> t -> t) -> e -> e -> t -> t;
	who23 = which2 person' : (e -> e -> e -> t -> t ) -> e -> e -> e -> t -> t;
	what = which0 thing' : (e -> t) -> e -> t -> t ;
	what12 = which1 thing' : (e -> e -> t -> t) -> e -> e -> t -> t;
	what23 = which2 thing' : (e -> e -> e -> t -> t) -> e -> e -> e -> t -> t;
	
	like' : e -> e -> t;
	barke' : e -> t;
	think' : t -> e -> t;

	k = t -> t:type;
	k1 = e -> t -> t:type;
	k2 = e -> e -> t -> t:type;
	k3 = e -> e -> e -> t -> t:type;

	wonder1' : k1 -> e -> t;
	wonder2' : k2 -> e -> t;

(*	who : (e -> t) -> k1;
	who12 : (e -> k1) -> k2 ;
	who23 :  (e -> k2) -> k3 ;
	what : (e -> t) -> k1;
	what12 : (e -> k1) -> k2;
	what23 : (e -> k2) -> k3 ; *)
end

lexicon RC_ty (semantics) : ty_n =
	e := e;
	t :=t;
	e_t_t:= (e ->t) -> t;

	k := k;
	k1 := k1;
	k2 := k2;
	k3 := k3;
	
	e_t_k1 := (e -> t) -> k1;
	e_k1_k2 := (e -> k1) -> k2;
	e_k2_k3 := (e -> k2) -> k3;

	G_q := lambda Q r. Q r;
	G_w12 := lambda Q r. Q r;
	G_w23 := lambda Q r. Q r;
	

	everybody':= everybody';
	somebody' := somebody';
	chris':= chris';
	kim':= kim';
	dana':= dana';
	sandy':= sandy';

	like' := like' ;
	barke' := barke';
	think' := think';

	wonder1' := wonder1' ;
	wonder2' := wonder2' ;


	whether' := whether' ;

	who_filler := who ;
	who_in_situ12 := who12 ;
	who_in_situ23 := who23 ;
	what_filler := what ;
	what_in_situ12 := what12;
	what_in_situ23 := what23 ;


end