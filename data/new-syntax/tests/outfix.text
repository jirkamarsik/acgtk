signature Abstract   = 
  case, gender, person, tense : type ;

  sub, obj : case ;

  f, m : gender ;

  p1, p2, p3 : person ;

  minus, plus : tense ;

  S : (tense) type ;

  N : (gender) type ;

  VP : (gender, person) type ;

  NP : (gender, person, case) type ;


  john : (c:case) NP m p3 c ;
  mary : (c:case) NP f p3 c ;

  i   : (g:gender) NP g p1 sub ;
  me  : (g:gender) NP g p1 obj ;
  you : (g:gender, c:case) NP g p2 c ;

  myself   : (g:gender, t:tense) 
             (NP g p1 obj -> NP g p1 sub -> S t) -> NP g p1 sub => S t ;
  yourself : (g:gender, t:tense) 
             (NP g p2 obj -> NP g p2 sub -> S t ) -> NP g p2 sub => S t ;
  herself  : (t:tense)
             (NP f p3 obj  -> NP f p3 sub -> S t ) -> NP f p3 sub => S t ;
  himself  : (t:tense)
             (NP m p3 obj -> NP m p3 sub -> S t ) -> NP m p3 sub  => S t  ;

  cook  : (g:gender) N g  ;
  man   : N m ;
  woman : N f  ;

  good : (g:gender) N g  -> N g ;

  a	: (g:gender, c:case, t:tense) N g  -> (NP g  p3  c -> S t ) -> S t ;
  every : (g:gender, c:case, t:tense) N g  -> (NP g  p3  c  -> S t ) -> S t  ; 

  be_0     : (g:gender, p_o:person, p_s:person)
              NP g p_o obj  -> NP g p_s sub  -> S minus  ;
  be_1     : (g:gender, p_o:person)
              NP g p_o obj  -> NP g p1 sub  -> S plus  ;
  be_2     : (g:gender, p_o:person)
              NP g p_o obj  -> NP g p2 sub  -> S plus  ;
  be_3     : (g:gender, p_o:person)
              NP g p_o obj  -> NP g p3 sub  -> S plus  ;
  find_0   : (g_o:gender, p_o:person, g_s:gender, p_s:person)
              NP g_o p_o obj  -> NP g_s p_s sub  -> S minus  ;
  find_1   : (g_o:gender, p_o:person, g_s:gender)
              NP g_o p_o obj  -> NP g_s p1 sub  -> S plus  ;
  find_2   : (g_o:gender, p_o:person, g_s:gender)
              NP g_o p_o obj  -> NP g_s p2 sub  -> S plus  ;
  find_3   : (g_o:gender, p_o:person, g_s:gender)
              NP g_o p_o obj  -> NP g_s p3 sub  -> S plus  ;
  love_0   : (g_o:gender, p_o:person, g_s:gender, p_s:person)
              NP g_o p_o obj  -> NP g_s p_s sub  -> S minus  ;
  love_1   : (g_o:gender, p_o:person, g_s:gender)
              NP g_o p_o obj  -> NP g_s p1 sub  -> S plus  ;
  love_2   : (g_o:gender, p_o:person, g_s:gender)
              NP g_o p_o obj  -> NP g_s p2 sub  -> S plus ;
  love_3   : (g_o:gender, p_o:person, g_s:gender)
              NP g_o p_o obj  -> NP g_s p3 sub  -> S plus  ;
  try_1    : (g:gender) VP g p1  -> NP g p1 sub  -> S plus  ;
  try_2    : (g:gender) VP g p2  -> NP g p2 sub  -> S plus  ; 
  try_3    : (g:gender) VP g p3  -> NP g p3 sub  -> S plus  ;

  to : (g:gender, p:person) (NP g p sub  -> S minus ) -> VP g p  ;

  who  : (g:gender) (NP g p3 sub  -> S plus ) -> N g  -> N g  ;
  whom : (g:gender) (NP g p3 obj  -> S plus ) -> N g  -> N g  

end



signature Vocabulary = 

 unit : type ;

 one : unit ;

 atom, s : type ;

(* string = s -> s;*)

infix +  : (s -> s) -> (s -> s) -> (s -> s);

 outfix [ . ] : atom -> (s -> s); 

(* infix + = lambda x y z. x (y z) : (s -> s) -> (s -> s) -> (s -> s);
*)



(* empty = lambda x. x : (s -> s); *)

 John, Mary, I, me, you, myself, yourself, herself, himself, cook,
 man, woman, good, a, every, be, am, are, is, find, finds, love,
 loves, try, tries, to, who, whom : s -> s

end

lexicon Synt_lex (Abstract) : Vocabulary =

  case   := unit ;
  gender := unit ;
  person := unit ;
  tense := unit ;

  sub   := one ;
  obj   := one ;
  f     := one ;
  m     := one ;
  p1    := one ;
  p2    := one ;
  p3    := one ;
  minus := one ;
  plus  := one ;
 
  S  := (x:unit) string ;
  N  := (x:unit) string ;
  VP := (x:unit, y:unit) string ;
  NP := (x:unit, y:unit, z:unit) string ;

  john := [John] ;
  mary := [Mary] ;

  i   := [I] ;
  me  := [me] ;
  you := [you] ;

  myself   := lambda f. Lambda x. f [ myself ] x ;

  yourself := lambda f. Lambda x. f [yourself] x ;
  herself  := lambda f. Lambda x. f [herself] x ;
  himself  := lambda f. Lambda x. f [himself] x ;

  cook  := [cook] ;
  man   := [man] ;
  woman := [woman] ;

  good := lambda x. [good] + x ;

  a	:= lambda x f. f ([a] + x) ;
  every := lambda x f. f ([every] + x) ;

  be_0   := lambda x y. y + ([be] + x) ;
  be_1   := lambda x y. y + ([am] + x) ;
  be_2   := lambda x y. y + ([are] + x) ;
  be_3   := lambda x y. y + ([is] + x) ;
  find_0 := lambda x y. y + ([find] + x) ;
  find_1 := lambda x y. y + ([find] + x) ;
  find_2 := lambda x y. y + ([find] + x) ;
  find_3 := lambda x y. y + ([finds] + x) ;
  love_0 := lambda x y. y + ([love] + x) ;
  love_1 := lambda x y. y + ([love] + x) ;
  love_2 := lambda x y. y + ([love] + x) ;
  love_3 := lambda x y. y + ([loves] + x) ;

  try_1  := lambda x y. y + ([try] + x) ;
  try_2  := lambda x y. y + ([try] + x) ;
  try_3  := lambda x y. y + ([tries] + x) ;

  to     := lambda x. [to] + (x empty) ;

  who  := lambda f x. x + ([who] + (f empty)) ;
  whom := lambda f x. x + ([whom] + (f empty))


end
